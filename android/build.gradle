import groovy.xml.XmlSlurper

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

rootProject.buildDir = "../build"
subprojects {
    project.buildDir = "${rootProject.buildDir}/${project.name}"
}
subprojects {
    project.evaluationDependsOn(":app")
}

subprojects { project ->
    project.plugins.withId("com.android.library") {
        def hasNamespace = project.android.hasProperty("namespace") && project.android.namespace
        if (hasNamespace) {
            return
        }

        def manifestFile = project.file("src/main/AndroidManifest.xml")
        if (!manifestFile.exists()) {
            return
        }

        def manifestPackage = new XmlSlurper().parse(manifestFile).@package?.text()
        if (manifestPackage) {
            project.android.namespace = manifestPackage
        }
    }
}

tasks.register("clean", Delete) {
    delete rootProject.buildDir
}
